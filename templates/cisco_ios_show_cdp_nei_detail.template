Value Required DESTINATION_HOST (\S+)
Value INTERFACE_IP (\d+\.\d+\.\d+\.\d+|\w+\.\w+\.\w+)
Value PLATFORM (.*)
Value REMOTE_PORT (.*)
Value LOCAL_PORT (.*)
Value SOFTWARE_VERSION (.*$)
Value CAPABILITIES (.*)
Value MANAGEMENT_IP (\d+\.\d+\.\d+\.\d+|\w+\.\w+\.\w+)

Start
  ^Device ID: ${DESTINATION_HOST}
  ^Entry address\(es\)\s*:\s* -> ParseIP
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}
  ^Interface: ${LOCAL_PORT},  Port ID \(outgoing port\): ${REMOTE_PORT}
  ^Version : -> GetVersion
  ^Management address\(es\)\s*:\s* -> GetManagementIP
  ^-------------------------.* -> Record
  ^Total cdp entries displayed.* -> Record

ParseIP
  ^.*IP address: ${INTERFACE_IP} -> Start
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES} -> Start

GetVersion
  ^${SOFTWARE_VERSION} -> Start
  
GetManagementIP
  ^.*IP address: ${MANAGEMENT_IP} -> Start

